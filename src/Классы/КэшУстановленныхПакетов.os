
#Использовать logos

Перем Лог;

Перем УстановленныеПакеты;

Функция ПакетУстановлен(Знач ОписаниеЗависимости) Экспорт
	
	ПакетУстановлен = УстановленныеПакеты.Получить(ОписаниеЗависимости.ИмяПакета) <> Неопределено;
	
	КаталогБиблиотек = ОбъединитьПути(КаталогПрограммы(), ПолучитьЗначениеСистемнойНастройки("lib.system"));
	ПутьКФайлуМетаданных = ОбъединитьПути(
		КаталогБиблиотек,
		ОписаниеЗависимости.ИмяПакета,
		КонстантыOpm.ИмяФайлаМетаданныхПакета
	);
	ФайлМетаданных = Новый Файл(ПутьКФайлуМетаданных);
	Если ФайлМетаданных.Существует() Тогда	
		МетаОписаниеПакета = ПрочитатьМетаданныеПакета(ПутьКФайлуМетаданных);
		УстановленнаяВерсия = МетаОписаниеПакета.Свойства().Версия;	
	Иначе
		// @deprecated
		УстановленнаяВерсия = 0;
	КонецЕсли;
	Лог.Отладка("ПакетУстановлен: Перед вызовом СравнитьВерсии(ЭтаВерсия = <%1>, БольшеЧемВерсия = <%2>)", УстановленнаяВерсия, ОписаниеЗависимости.МинимальнаяВерсия);
	
	УстановленаКорректнаяВерсия = ОписаниеЗависимости.МинимальнаяВерсия = Неопределено
		ИЛИ РаботаСВерсиями.СравнитьВерсии(УстановленнаяВерсия, ОписаниеЗависимости.МинимальнаяВерсия) = 1;
	
	Возврат ПакетУстановлен И УстановленаКорректнаяВерсия;
	
КонецФункции

Процедура Обновить() Экспорт
	
	КаталогБиблиотек = ОбъединитьПути(КаталогПрограммы(), ПолучитьЗначениеСистемнойНастройки("lib.system"));
	Лог.Отладка("КаталогБиблиотек " + КаталогБиблиотек);
	УстановленныеПакеты = Новый Соответствие;
	
	НайденныеФайлы = НайтиФайлы(КаталогБиблиотек, ПолучитьМаскуВсеФайлы());
	Для Каждого ФайлКаталога Из НайденныеФайлы Цикл
		Если ФайлКаталога.ЭтоКаталог() Тогда
			ДобавитьУстановленныйПакет(ФайлКаталога);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьУстановленныеПакеты() Экспорт
	Возврат УстановленныеПакеты;
КонецФункции

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог("oscript.app.opm");
	//Лог.УстановитьУровень(УровниЛога.Отладка);
	
	Обновить();
	
КонецПроцедуры

Процедура ДобавитьУстановленныйПакет(Знач ФайлКаталога)
	
	ПутьКФайлуМетаданных = ОбъединитьПути(ФайлКаталога.ПолноеИмя, КонстантыOpm.ИмяФайлаМетаданныхПакета);
	ФайлМетаданных = Новый Файл(ПутьКФайлуМетаданных);
	Если ФайлМетаданных.Существует() Тогда	
		МетаОписаниеПакета = ПрочитатьМетаданныеПакета(ПутьКФайлуМетаданных);	
	Иначе
		// @deprecated
		МетаОписаниеПакета = Истина;
	КонецЕсли;
	
	УстановленныеПакеты.Вставить(ФайлКаталога.Имя, МетаОписаниеПакета);
	
КонецПроцедуры

Функция ПрочитатьМетаданныеПакета(Знач ПутьКФайлуМетаданных)
	
	Перем Метаданные;
	Попытка
		Чтение = Новый ЧтениеXML;
		Чтение.ОткрытьФайл(ПутьКФайлуМетаданных);
		Сериализатор = Новый СериализацияМетаданныхПакета;
		Метаданные = Сериализатор.ПрочитатьXML(Чтение);
		
		Чтение.Закрыть();
	Исключение
		Чтение.Закрыть();
		ВызватьИсключение;
	КонецПопытки;

	Возврат Метаданные;
	
КонецФункции

Инициализация();
