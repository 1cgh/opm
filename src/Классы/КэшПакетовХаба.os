
#Использовать logos

Перем Лог;

Перем ПакетыХаба;

Процедура Обновить() Экспорт

	УстановкаПакета = Новый Установкапакета();
	Сервер =  Константы.СерверУдаленногоХранилища;
	Соединение = УстановкаПакета.ИнициализироватьСоединение(Сервер);
	Ресурс = Константы.ПутьВХранилище + "list.txt";
	Запрос = Новый HTTPЗапрос(Ресурс);
	Ответ  = Соединение.Получить(Запрос);

	Если НЕ Ответ.КодСостояния = 200 Тогда
		ТекстИсключения = СтрШаблон("Ошибка подключения к хабу <%1>", Ответ.КодСостояния);
		Ответ.Закрыть();
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	HTMLхаба = Ответ.ПолучитьТелоКакСтроку();
	ВремФайл = ОбъединитьПути(КаталогВременныхФайлов(), "list.txt");
	Ответ.ПолучитьТелоКакДвоичныеДанные().Записать(ВремФайл);
	Ответ.Закрыть();

	ЧтениеТекста = Новый ЧтениеТекста();
	ЧтениеТекста.Открыть(ВремФайл);
	ПакетыХаба = Новый Соответствие;

	СтрокаТекста = ЧтениеТекста.ПрочитатьСтроку();
	Пока СтрокаТекста <> Неопределено Цикл
		ПакетыХаба.Вставить(СтрокаТекста, Истина);
		СтрокаТекста = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла;		

	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ВремФайл);
	
КонецПроцедуры

Функция ЭтоПакетХаба(Знач ИмяПакета) Экспорт

	Возврат ПакетыХаба[ИмяПакета] = Истина;

КонецФункции

Функция ПолучитьПакетыХаба() Экспорт

	Возврат ПакетыХаба;

КонецФункции	

Процедура Инициализация()

	Лог = Логирование.ПолучитьЛог("oscript.app.opm");
	//Лог.УстановитьУровень(УровниЛога.Отладка);
	
	Обновить();
	
КонецПроцедуры

Инициализация();
