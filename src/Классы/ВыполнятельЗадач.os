
Процедура ВыполнитьЗадачу(Знач ИмяЗадачи, Знач ПараметрыЗадачи) Экспорт
	
	Если ПараметрыЗадачи = Неопределено Тогда
		ПараметрыЗадачи = Новый Массив;
	КонецЕсли;

	ПутьККаталогуЗадач = "";
	
	ОписаниеПакета = РаботаСОписаниемПакета.ПрочитатьОписаниеПакета();
	Свойства = ОписаниеПакета.Свойства();
	Если Свойства.Свойство("Задачи") Тогда
		ПутьККаталогуЗадач = Свойства.Задачи;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПутьККаталогуЗадач) Тогда
		ПутьККаталогуЗадач = ОбъединитьПути(ТекущийКаталог(), "tasks");
	КонецЕсли;

	КаталогЗадач = Новый Файл(ПутьККаталогуЗадач);
	Если НЕ КаталогЗадач.Существует() Тогда
		ТекстСообщения = СтрШаблон("Не найден каталог задач: %1", КаталогЗадач.ПолноеИмя);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	ПутьКЗадаче = ОбъединитьПути(ПутьККаталогуЗадач, ИмяЗадачи + ".os");
	ПараметрыСценария = Новый Структура("ПараметрыЗадачи", ПараметрыЗадачи);
	ЗагрузитьСценарий(ПутьКЗадаче, ПараметрыСценария);

КонецПроцедуры

