/////////////////////////////////////////////////////////////////////////
//
// OneScript Package Manager
// Установщик пакетов для OneScript
// Выполняется, как os-приложение в командной строке:
//
// opm install my-package.ospx
//
/////////////////////////////////////////////////////////////////////////

#Использовать cmdline
#Использовать logos

#Использовать "."

Перем Лог;

Процедура ВыполнитьКоманду(Знач Аргументы)
	
	ОбработкаКоманд = СоздатьОбработчикКоманд();
	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	
	ОбработкаКоманд.ДобавитьОписанияКоманд(Парсер);
	
	ПараметрыКоманды = Парсер.РазобратьКоманду(Аргументы);
	Если ПараметрыКоманды = Неопределено Тогда
		ВывестиСправкуПоКомандам(ОбработкаКоманд);
		ЗавершитьРаботу(1);
	КонецЕсли;
	
	Попытка
		ОбработкаКоманд.ВыполнитьКоманду(ПараметрыКоманды);
	Исключение
		Лог.Отладка(ОписаниеОшибки());
		Лог.Информация(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗавершитьРаботу(1);
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиСправкуПоКомандам(Знач ОбработкаКоманд)
	
	ОбработкаКоманд.ВывестиСправкуПоКомандам();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Вспомогательные функции

Функция СоздатьОбработчикКоманд()
	Возврат Новый ДиспетчерКомандПриложения();
КонецФункции

Процедура Инициализация()

	ОсновноеЗеркало = Новый Зеркало;
	ОсновноеЗеркало.СерверУдаленногоХранилища = Константы.СерверУдаленногоХранилища;
	ОсновноеЗеркало.ПутьВХранилище            = Константы.ПутьВХранилище;

	ЗапасноеЗеркало = Новый Зеркало;
	ЗапасноеЗеркало.СерверУдаленногоХранилища = Константы.СерверЗапасногоХранилища;
	ЗапасноеЗеркало.ПутьВХранилище            = Константы.ПутьВЗапасномХранилище;

	Зеркала.Добавить(ОсновноеЗеркало);
	Зеркала.Добавить(ЗапасноеЗеркало);

КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Точка входа

Лог = Логирование.ПолучитьЛог(Константы.ИмяЛога);
НастройкиПриложения.УстановитьФайлНастроек(ОбъединитьПути(СтартовыйСценарий().Каталог, "opm.cfg"));
Инициализация();

ВыполнитьКоманду(АргументыКоманднойСтроки);
